#========[¤]========#
#                   #
#       Tasks       #
#                   #
#========[¤]========#
#
# This file is synced over the BungeeCord Network
# 	and should therefore ONLY execute tasks which CAN
#	affect players on join on ANY server of the network.
#

#
# Utilities
#
command /formattedservermessages <player> <text>:
	executable by: console
	trigger:
		send "&7%arg 2%" to arg 1

#
# GUI
#
command /openbankgui <offlineplayer>:
	executable by: console
	trigger:
		make arg 1 run command "bank open" as op

on inventory click:
	if "%clicked slot type%" is "result":
		cancel event
		make player execute command "menu"
	else if "%clicked slot type%" is "crafting":
		if clicked slot is 1:
			cancel event
			make player execute command "teleportations"
		else if clicked slot is 2:
			cancel event
			make player execute command "journal"
			message "&7Vous recevez/mettez à jour votre journal."
			message "&7Il devrait se trouver dans votre inventaire."
		else if clicked slot is 3:
			cancel event
			make player execute command "progression"
		else if clicked slot is 4:
			cancel event
			make player execute command "sac"

#
# Aliases
#

#backpack
command /sac:
	trigger:
		make player execute command "backpack"

#
# Rerouted & Forbidden Commands
#
on command:
	if command is "mp":
		cancel event
		make player execute command "msg %arguments%"
	
	else if command contains "market":
		if executor does not have permission "market.temp":
			cancel event
			message "&7Pour accéder au marché, parlez à un commerçant dans un marché."


command /custommarktsell <player>:
	executable by: console
	trigger:
		open an anvil gui named "MarketSell" to arg 1 with default text "Vendre objet en main : prix ?"

on close anvil gui named "MarketSell":
	set {_integer} to event-text parsed as integer
	if {_integer} is integer:
		execute command "lp user %player% permission settemp market.temp true 3seconds"
		set {_integer} to event-text parsed as integer
		wait 1 second
		make player execute command "market sell %{_integer}%"
		message "&7Vous avez mis votre objet en vente sur le marché pour un prix de %{_integer}%¤."
	else:
		message "&7Merci de préciser le prix entier auquel vous souhaitez vendre l'objet que vous tenez actuellement en main."


#
# Forbidden Actions
#
on craft:
	if "%event-slot%" contains "crafting slot 0":
		if gamemode of player is survival:
			cancel event
			message "&7Vous ne pouvez pas faire cela."

on right click on crafting table:
	if player is not sneaking:
		cancel event
		execute console command "ecraft open main %player%"

on right click on enchantment table:
	cancel event

on right click on anvil:
	cancel event
	execute console command "ecraft open anvil %player%"

on right click on furnace:
	if nbt of event-block contains "Haut-Fourneau":
		cancel event
		execute console command "ecraft open furnace %player%"

on smelt:
	if event-block's ore slot is any ore:
		cancel event

#
# Vehicule Management
#
on vehicle exit:
	set {_vehicle} to vehicle of player
	if "%{_vehicle}%" contains "boat":
		delete vehicle of player
		if "%{_vehicle}%" contains "dark oak":
			drop 1 dark oak boat at event-location
		else if "%{_vehicle}%" contains "oak":
			drop 1 oak boat at event-location
			# The spruce boat appears to be bugged => on entering, it opens an anvil for some reason.
		else if "%{_vehicle}%" contains "spruce":
			drop 1 spruce boat at event-location
		else if "%{_vehicle}%" contains "jungle":
			drop 1 jungle boat at event-location
		else if "%{_vehicle}%" contains "acacia":
			drop 1 acacia boat at event-location
		else if "%{_vehicle}%" contains "birch":
			drop 1 birch boat at event-location
	else if "%{_vehicle}%" contains "minecart":
		delete vehicle of the player
		drop 1 minecart at event-location

#
# Custom texture for specific blocks
#
on block place:
	if event-block is furnace:
		if nbt of event-block contains "Haut":
			set biome at event-block to the void

on block break:
	if event-block is furnace:
		if nbt of event-block contains "Haut":
			if "%biome north of event-block%" contains "void":
				if "%biome south of event-block%" contains "void":
					if "%biome east of event-block%" contains "void":
						if "%biome west of event-block%" contains "void":
							log "Problème de biome suite à destruction de four en %event-location%, %event-world%  - Erreur 1618-TheVoid." to "errors.log"
						else:
							set biome at event-block to biome west of event-block
					else:
						set biome at event-block to biome east of event-block
				else:
					set biome at event-block to biome south of event-block
			else:
				set biome at event-block to biome north of event-block

#
# SideBar
#
every 10 seconds:
	async
	loop all players:
		set {_player} to loop-player
		set tablist header of bungeecord player {_player} to "&7&nGaïa Renaissance%nl%&7Comptes uniques : %placeholder ""server_unique_joins"" from {_player}%" and "&7Temps de jeu : %placeholder ""statistic_time_played"" from {_player}%%nl%&7Ping : %placeholder ""player_ping"" from {_player}% &8| &7Votre IP : %placeholder ""player_ip"" from {_player}%%nl%&7TPS : %placeholder ""server_tps_1"" from {_player}% &8(/1sec) | &7%placeholder ""server_tps_5"" from {_player}% &8(/5sec)%nl%&7------------------ &8¤ &7------------------"

		wipe {_player}'s sidebar
		set name of sidebar of {_player} to "&7&n%{_player}%&r&7 (Race : &8%RaceCheck({_player})%&7)"
		set score "&7%placeholder ""heroes_class"" from {_player}% (Niv.&8%placeholder ""heroes_class_level"" from {_player}%&7/&8%placeholder ""heroes_class_maxlevel"" from {_player}%&7)" in sidebar of {_player} to 100
		set score "&8%placeholder ""heroes_class_exp"" from {_player}%&7/&8%placeholder ""heroes_class_maxexp"" from {_player}%&7xp (&8%placeholder ""heroes_class_exp_percent"" from {_player}%&7%%)" in sidebar of {_player} to 99
		set score "&7%placeholder ""heroes_class2"" from {_player}% (Niv.&8%placeholder ""heroes_class2_level"" from {_player}%&7/&8%placeholder ""heroes_class2_maxlevel"" from {_player}%&7)" in sidebar of {_player} to 98
		set score "&8%placeholder ""heroes_class2_exp"" from {_player}%&7/&8%placeholder ""heroes_class2_maxexp"" from {_player}%&7xp (&8%placeholder ""heroes_class2_exp_percent"" from {_player}%&7%%)" in sidebar of {_player} to 97
		set score "&7&nMétiers&r&7 (Niv.&8%placeholder ""jobs_total_levels"" from {_player}%&7)" in sidebar of {_player} to 90
		if placeholder "jobs_level_Bûcheron" from {_player} is set:
			set score "&7Bûcheron (Niv.&8%placeholder ""jobs_level_Bûcheron"" from {_player}%&7/&8%placeholder ""jobs_max_level_Bûcheron"" from {_player}%&7)" in sidebar of {_player} to 85
		if placeholder "jobs_level_Mineur" from {_player} is set:
			set score "&7Mineur (Niv.&8%placeholder ""jobs_level_Mineur"" from {_player}%&7/&8%placeholder ""jobs_max_level_Mineur"" from {_player}%&7)" in sidebar of {_player} to 80
		if placeholder "jobs_level_Excavateur" from {_player} is set:
			set score "&7Excavateur (Niv.&8%placeholder ""jobs_level_Excavateur"" from {_player}%&7/&8%placeholder ""jobs_max_level_Excavateur"" from {_player}%&7)" in sidebar of {_player} to 75
		if placeholder "jobs_level_Constructeur" from {_player} is set:
			set score "&7Constructeur (Niv.&8%placeholder ""jobs_level_Constructeur"" from {_player}%&7/&8%placeholder ""jobs_max_level_Constructeur"" from {_player}%&7)" in sidebar of {_player} to 70
		if placeholder "jobs_level_Fermier" from {_player} is set:
			set score "&7Fermier (Niv.&8%placeholder ""jobs_level_Fermier"" from {_player}%&7/&8%placeholder ""jobs_max_level_Fermier"" from {_player}%&7)" in sidebar of {_player} to 65
		if placeholder "jobs_level_Éleveur" from {_player} is set:
			set score "&7Éleveur (Niv.&8%placeholder ""jobs_level_Éleveur"" from {_player}%&7/&8%placeholder ""jobs_max_level_Éleveur"" from {_player}%&7)" in sidebar of {_player} to 60
		if placeholder "jobs_level_Chasseur" from {_player} is set:
			set score "&7Chasseur (Niv.&8%placeholder ""jobs_level_Chasseur"" from {_player}%&7/&8%placeholder ""jobs_max_level_Chasseur"" from {_player}%&7)" in sidebar of {_player} to 55
		if placeholder "jobs_level_Artisan" from {_player} is set:
			set score "&7Artisan (Niv.&8%placeholder ""jobs_level_Artisan"" from {_player}%&7/&8%placeholder ""jobs_max_level_Artisan"" from {_player}%&7)" in sidebar of {_player} to 50
		if placeholder "jobs_level_Pêcheur" from {_player} is set:
			set score "&7Pêcheur (Niv.&8%placeholder ""jobs_level_Pêcheur"" from {_player}%&7/&8%placeholder ""jobs_max_level_Pêcheur"" from {_player}%&7)" in sidebar of {_player} to 45
		if placeholder "jobs_level_Forgeron" from {_player} is set:
			set score "&7Forgeron (Niv.&8%placeholder ""jobs_level_Forgeron"" from {_player}%&7/&8%placeholder ""jobs_max_level_Forgeron"" from {_player}%&7)" in sidebar of {_player} to 40

		#set score "&7&nCoordonnées : " in sidebar of {_player} to 30
		#set score "&8%placeholder ""player_x"" from {_player}%&7,&8%placeholder ""player_y"" from {_player}%&7,&8%placeholder ""player_z"" from {_player}%" in sidebar of {_player} to 29

